//@version=3
study(title = "Elliott Wave Counter[MASK MAN]",shorttitle = "EW cnt", overlay=true)
len      = input(title="Length"      ,defval=10)
is4      = input(title="EW4>EW1"     ,defval=true)
plot_zig = input(title="plot zigzag" ,defval=true)
age      = input(title="up:check , down:uncheck",defval=true)

hi_ss = age ? high : -high
lo_ss = age ? low  : -low
phi=pivothigh(hi_ss,len,len)
plo=pivotlow(lo_ss ,len,len)

state     = 0
state    := phi ? 1 : plo ? 0 : state[1]
hi        = state == 1
lo        = state == 0
hi_update = lo[1] and phi
lo_update = hi[1] and plo

hi0  = close
hi1  = close
lo0  = close
lo1  = close
hi0 := phi       ? hi_ss[len] : hi0[1]
hi1 := hi_update ? hi0[1]     : hi1[1]
lo0 := plo       ? lo_ss[len] : lo0[1]
lo1 := lo_update ? lo0[1]     : lo1[1]

EW = 0
if (EW[1] == 0)
    if hi_update
        EW := (hi0 > lo0) ? 1 : 0
    else
        EW := 0
else
    if (EW[1] == 1)
        if lo_update
            EW := (hi0 > lo0 and lo0 > lo1) ? 2 : 0
        else
            EW := 1
    else
        if (EW[1] == 2)
            if hi_update
                EW := (hi0 > lo0 and hi0 > hi1) ? 3 : 1
            else
                EW := 2
        else
            if (EW[1] == 3)
                ew4over1 = is4 ? lo0 > hi1 : true
                if lo_update
                    EW := (hi0 > lo0 and lo0 > lo1 and ew4over1) ? 4 : 0
                else
                    EW := 3
            else
                if (EW[1] == 4)
                    if hi_update
                        EW := (hi0 > lo0 and hi0 > hi1) ? 5 : 1
                    else
                        EW := 4
                else
                    if (EW[1] == 5)
                        if lo_update
                            EW := (hi0 > lo0 and hi0 > hi1) ? 6 : 0
                        else
                            EW := 5
                    else

                        if (EW[1] == 6)
                            if hi_update
                                EW := (hi0 > lo0 and hi1 > hi0) ? 7 : 1
                            else
                                EW := 6
                        else
                            if (EW[1] == 7)
                                if lo_update
                                    EW := (hi0 > lo0 and lo1 > lo0) ? 8 : 0
                                else
                                    EW := 7
                            else
                                if (EW[1] == 8)
                                    if hi_update
                                        EW := (hi0 > lo0) ? 1 : 8
                                    else
                                        EW := 8

EW0 = plo ? EW == 0 : na
EW1 = phi ? EW == 1 : na
EW2 = plo ? EW == 2 : na
EW3 = phi ? EW == 3 : na
EW4 = plo ? EW == 4 : na
EW5 = phi ? EW == 5 : na
EWa = plo ? EW == 6 : na
EWb = phi ? EW == 7 : na
EWc = plo ? EW == 8 : na

plotshape(EW0?true:na,style=shape.triangledown,text="0",color=black,location=location.belowbar,offset=-len)
plotshape(EW1?true:na,style=shape.triangledown,text="1",color=black,location=location.abovebar,offset=-len)
plotshape(EW2?true:na,style=shape.triangledown,text="2",color=black,location=location.belowbar,offset=-len)
plotshape(EW3?true:na,style=shape.triangledown,text="3",color=black,location=location.abovebar,offset=-len)
plotshape(EW4?true:na,style=shape.triangledown,text="4",color=black,location=location.belowbar,offset=-len)
plotshape(EW5?true:na,style=shape.triangledown,text="5",color=black,location=location.abovebar,offset=-len)
plotshape(EWa?true:na,style=shape.triangledown,text="A",color=black,location=location.belowbar,offset=-len)
plotshape(EWb?true:na,style=shape.triangledown,text="B",color=black,location=location.abovebar,offset=-len)
plotshape(EWc?true:na,style=shape.triangledown,text="C",color=black,location=location.belowbar,offset=-len)

zigzag = plot_zig ? phi ? high[len] : plo ? low[len] : na : na
plot(zigzag,color=black,offset=-len,linewidth=2)
